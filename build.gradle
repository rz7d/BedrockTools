buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.2.0." + getRevision()
group = "com.github.stilllogic20.bedrocktools"
archivesBaseName = "bedrocktools"

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
compileJava {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run"
    mappings = "snapshot_20180814"
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.4.2"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.4.2"
}

test {
    useJUnitPlatform()
}

processResources {
    inputs.property "name", name
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

def getRevision() {
    def out = new StringBuilder()
    def git = "git.exe rev-parse --short HEAD".execute()
    git.consumeProcessOutput(out, System.err)
    git.waitForOrKill(2000)
    return out.toString().replaceAll("\r?\n", "")
}

